; タンクバタリアン (c)ナムコ 用
; メインルーチン

	cseg
	org	0400h
stack:	equ	0f380h		; スタックエリア

; メインループ
main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:

	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	in	a,(03h)		; Code No.

	cp	007h
	jr	nc,loop

	di

	ld	hl,MUSIC_TABLE
	ld	b,a
	add	a,a
	add	a,a
	add	a,b
	add	a,b
	ld	b,000h
	ld	c,a
	add	hl,bc

	ld	b,003h
	ld	a,001h
SET_ADDR:
	ld	e,(hl)
	inc	hl
	ld	d,(hl)
	ld	(de),a
	inc	hl
	djnz	SET_ADDR

	ei

	xor	a
	out	(02h),a

	jr	loop

stop:

	di

	xor	a
	out	(02h),a

	ld	hl,0ea00h
	ld	(hl),000h
	ld	de,0ea01h
	ld	bc,00400h
	ldir

	call	00090h

	ei

	jr	loop

init:
	ld	a,0c3h
	ld	hl,058a9h
	ld	(0FD9Fh), a
	ld	(0FDA0h), hl

	ld	a, 0C9h
	ld	(0594Dh), a
	call	00090h
	ret

MUSIC_TABLE:
	dw	0ea00h,	0ea01h,	0ea02h
	dw	0ea03h,	0ea04h,	0ea05h
	dw	0ea06h,	0ea06h,	0ea06h
	dw	0ea07h,	0ea07h,	0ea07h
	dw	0ea08h,	0ea08h,	0ea08h
	dw	0ea09h,	0ea09h,	0ea09h
	dw	0ea0ah,	0ea0ah,	0ea0ah
