; 吉田コンツェルン (c)ASCII/MSX Magazine
;
; @autor RuRuRu
; @date 2012/12/13 1st Release
; @date 2024/01/12 Fix user tones for MUS00~03

	cseg
	org	0400h

stack:	equ	0f380h
mdata:	equ	0b400h

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; Key wait
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	call	0D406h		; Stop
	ei
	halt

	di
	in	a,(03h)		; Code No.
	out	(03h), a
	ei

	cp	00h
	jr	z,b00
	cp	01h
	jr	z,b01
	cp	02h
	jr	z,b02
	cp	03h
	jr	z,b02

	ld	hl, mdata
pbgm:
	in	a,(04h)		; Loop
	call	0D403h		; Play
	ei
	jr	loop

b00:	; MUS00.BIN
	ld	hl, 0B400h
	ld	de, 0C400h
	ld	bc, 00200h
	ldir
	ld	hl, 0C400h
	jr	pbgm

b01:	; MUS01.BIN
	ld	hl, 0B400h
	ld	de, 0CA00h
	ld	bc, 00600h
	ldir
	ld	hl, 0CA00h
	jr	pbgm

b02:	; MUS02.BIN / MUS03.BIN
	ld	hl, 0B400h
	ld	de, 0C000h
	ld	bc, 00C00h
	ldir
	ld	hl, 0C000h
	jr	pbgm

stop:
	call	0D406h		; Stop
	xor	a
	out	(02h),a		; Change hoot status
	jr	loop

init:
	ld	a, 0c3h
	ld	(0FD9Fh), a
	ld	hl, irq
	ld	(0FDA0h), hl

	ld	a, 03Fh		; m-slot : ram/ram/ram/bios
	out	(0A8h),a
	ld	a, 04h
	ld	(0FFFFh),a	; s-slot : bios/fmbios/none/none
	ld	a, 0F3h		; m-slot : ram/fmbios/ram/ram
	out	(0A8h),a

	call	0D400h		; Init

	ret

irq:
	call	0D40Ch
	ret
