; 吉田コンツェルン (c)ASCII/MSXマガ 用
; メインルーチン

	cseg
	org	0400h

stack:	equ	0f380h		; スタックエリア
mdata:	equ	0b400h		; 曲データアドレス

; メインループ

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	call	0D406h		; Stop
	ei
	halt

	di
	in	a,(03h)		; Code No.
	out	(03h), a
	ei

	in	a,(04h)		; Loop
	ld	hl, mdata
	call	0D403h		; Play
	ei
	jr	loop

stop:
	call	0D406h		; Stop
	xor	a
	out	(02h),a		; hoot status変更
	jr	loop

init:
	ld	a, 0c3h
	ld	(0FD9Fh), a
	ld	hl, irq
	ld	(0FDA0h), hl

	ld	a, 03Fh		; m-slot : ram/ram/ram/bios
	out	(0A8h),a
	ld	a, 04h
	ld	(0FFFFh),a	; s-slot : bios/fmbios/none/none
	ld	a, 0F3h		; m-slot : ram/fmbios/ram/ram
	out	(0A8h),a

	call	0D400h		; Init

	ret

irq:
	call	0D40Ch
	ret
