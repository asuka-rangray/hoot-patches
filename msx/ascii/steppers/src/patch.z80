; Stepers (c)ASCII 用
; メインルーチン

	cseg
	org	0400h

stack:	equ	0f000h		; スタックエリア

; メインループ

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

;	call	play_stop

play_start:
	in	a, (03h)	; Code No.
	cp	00
	jr	z, play_bgm
	cp	01
	jr	z, play_clear
	cp	02
	jr	z, play_miss

	dec	a
	dec	a
	dec	a

	di
	add	a, a		; x2
	ld	e, a
	ld	d, 00h
	ld	hl, mustbl
	add	hl, de
	ld	e,(hl)
	inc	hl
	ld	d,(hl)
	ex	de,hl
	call	0875Eh
	jr	play_end

play_bgm:
	xor	a
	ld	(0E923h),a	; counter
	ld	a, 1
	ld	(flg_bgm),a
	jr	play_end

play_clear:
	ld	ix,0988AH	; clear
	jr	play_bgm2

play_miss:
	ld	ix,0985AH	; miss

play_bgm2:
	xor	a
	ld	(flg_bgm),a
	call	08459h
	ld	b,010H
s_lp:
	call	08432h
	djnz	s_lp
	call	090h
	jr	play_end

stop:
	call	play_stop

play_end:
	ei
	xor	a		; hoot status変更
	out	(02h),a
	jr	loop

play_stop:
	di
	xor	a
	ld	(flg_bgm),a
	call	08781h
	call	0878Bh
	ei
	ret

init:
	ld	a, 0C3h
	ld	hl, irq
	ld	(0FD9Fh), a
	ld	(0FDA0h), hl

	call	08781h
	ret

irq:
	ld	hl, 0FC9Eh		; BIOS WORKを無理やり動かす
	ld	a, (hl)			; Wait用
	inc	a,
	ld	(hl),a

	call	08771h			; SE irq
	ld	a,(flg_bgm)
	or	a
	call	nz,096C1h		; BGM irq
	ret

flg_bgm:
	db	00h

mustbl:
	dw	0983Ch			; 00
	dw	0981Ah
	dw	097FBh
	dw	0984Ch
	dw	0982Bh

setbl:
	dw	097CCh			; 05
	dw	097BFh
	dw	097D3h
	dw	097DEh

