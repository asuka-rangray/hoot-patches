; ぺんぎんくんウォーズ2 (c)ASCI 用
; メインルーチン

	cseg
	org	0400h
stack:	equ	0f100h		; スタックエリア

; メインループ
main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:

	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	ld	a, 0FFh
	ld	(0EF89h),a	; Sound Code

	di
	in	a,(03h)		; Code No.
	ld	(0EF88h),a
	ei

	jr	play_end

stop:
	di
	ld	a,0FFh
	ld	(0ef88h),a
	ei

play_end:
	xor	a
	out	(02h),a
	jr	loop

init:
	ld	a, 0C9h
;	ld	(000Ch), a
;	ld	(0014h), a
	ld	(0024h), a

	; まず8000~BFFFをRAMにしないとBxxxに書けない
	ld	a, 0F7h		; m-slot : ram/rom/ram/ram
	out	(0A8h),a

	ld	hl,04030h	; copy to driver
	ld	de,0B400h
	ld	bc,03BFFh
	ldir

	; slot setting
	ld	a, 0F3h		; m-slot : ram/fm/ram/ram
	ld	(0B23Bh),a
	ld	a, 0F7h		; m-slot : ram/rom/ram/ram
	ld	(0B23Ah),a

	in	a, (07h)
	ld	(0B23Dh), a	; opll flag
	cp	00h
	jr	z, init_drv

; opll bios
	; subslot setting (opll)
	ld	a, 013h		; m-slot : ram/rom/rom/bios
	out	(0A8h),a
	ld	a, 04h
	ld	(0FFFFh),a	; s-slot : bios/fm/none/none
	ld	a,(0B23AH)	; OPLL slot
	out	(0A8h),a

init_drv:
	call	0E660h		; init
	ld	a, 0FFh
	ld	(0EF88h),a	; Sound Code

	ld	a,0c3h
	ld	(0FD9Fh), a
	ld	hl,irq
	ld	(0FDA0h), hl

	call	0C0E0h
	ret

irq:
	di
	ld	a,(0B23BH)	; OPLL slot
	out	(0A8h),a

	in	a, (07h)
	cp	00h
	jr	z, psg_irq

	call	0E708H		; OPL IRQ
	call	0411FH		; OPLDRV
	jr	irq_end

psg_irq:
	call	0E930h

irq_end:
	ld	a,(0B23AH)	; OPLL slot
	out	(0A8h),a
	ei
	ret
