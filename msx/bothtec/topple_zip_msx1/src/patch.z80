; Topple Zip(MSX1) (c)Bothec (CRC32:190f4ce5)
;
; @autor RuRuRu
; @date 2014/02/01 1st Release
; @date 2017/01/05 Support SE
;

	cseg
	org	0400h

stack:	equ	0f380h		; スタックエリア

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	in	a,(04h)
	cp	01h
	jr	nz,play_se

	di
	xor	a
	ld	(0C04Eh),a
	ld	(0C05Eh),a
	ld	(0C06Eh),a

	in	a,(03h)
	ld	(0C08Bh),a

	di
	ld	a, 0C3h
	ld	(0FD9Fh), a
	ei

	jr	play_end

play_se:
	ld	hl, play_end
	push	hl

	in	a,(03h)
	add	a, a		; x2
	ld	e, a
	ld	d, 00h
	ld	hl, setbl
	add	hl, de

	ld	e,(hl)
	inc	hl
	ld	d,(hl)
	ex	de,hl
	jp	(hl)

setbl:
	dw	07BA8h
	dw	07BBCh
	dw	07BC6h
	dw	07BD1h
	dw	07BD8h
	dw	07BDDh
	dw	07BE2h
	dw	07BE9h
	dw	07C01h
	dw	07C0Ch
	dw	07C11h
	dw	07C37h

stop:
	di
	ld	a, 0C9h
	ld	(0FD9Fh), a
	call	0090h
	ei

play_end:
	xor	a		; hoot status変更
	out	(02h),a
	jr	loop

init:
	ld	a, 0C9h
	ld	(0FD9Fh),a
	ld	hl, 077D0h
	ld	(0FDA0h),hl
	ld	a, 0B8h
	ld	(0C014h),a	; Reg7 value
	call	090h
	ret
