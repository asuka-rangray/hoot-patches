; 上海 II (c)SystemSoft
;
; @autor RuRuRu
; @date 2010/05/08 1st Release
;

	cseg
	org	00400h

stack:	equ	0f380h		; スタックエリア
mdata:	equ	09000h		; 曲データアドレス

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei
	jp	loop

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	call	0866Ah

	di
	in	a,(03h)
	out	(03h),a		; 曲データを読み込む
	ei

	ld	a,001H
	call	08660h

	jr	loop

stop:
	call	0866Ah

	xor	a		; 停止処理
	out	(02h),a
	jr	loop

init:
	ld	a, 0c3h
	ld	(001Ch), a
	ld	(0038h), a
	ld	(0093h), a
	ld	(0096h), a

	ld	hl, calslt
	ld	(001Dh), hl

	ld	hl, timer
	ld	(0039h), hl

	ld	hl, psgw
	ld	(0094h), hl

	ld	hl, psgr
	ld	(0097h), hl

	call 	09B18h

	ld	hl,mdata
	ld	a,001H
	call	08673h

	ret

calslt:
	jp	(ix)

psgw:
	di
	out     (0A0h),a
	push    af
	ld      a,e
	out     (0A1h),a
	ei
	pop     af
	ret

psgr:
	out     (0A0h),a
	in      a,(0A2h)
	ret

timer:
	push	hl
	push	de
	push	bc
	push	af
	exx
	ex	af,af'
	push	hl
	push	de
	push	bc
	push	af
	push	iy
	push	ix
	call	08683h
	pop	ix
	pop	iy
	pop	af
	pop	bc
	pop	de
	pop	hl
	ex	af,af'
	exx
	pop	af
	pop	bc
	pop	de
	pop	hl
	ei
	ret
