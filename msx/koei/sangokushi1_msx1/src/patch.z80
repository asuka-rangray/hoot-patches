; 三國志(MSX1 ROM) (c)KOEI
;
; @autor RuRuRu
; @date 2015/09/20 1st Release
;

	cseg
	org	0400h

stack:	equ	0f000h		; スタックエリア

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

; play
	in	a,(03h)
	ld	e,a
	add	a,a
	add	a,e
	ld	l,a
	ld	h,000H
	ld	de,mustbl
	add	hl,de
	ld	e,(hl)
	inc	hl
	ld	d,(hl)
	inc	hl
	ld	a,(hl)
	ld	(07000h),a	; bank change
	inc	a
	ld	(07800h),a	; bank change

	xor	a
	ld	(flg_stop),a
	in	a,(04h)
	ld	l, a
	ld	h, 00h
	call	07B1Ah		; Play
	jp	play_end

play_end:
	xor	a
	out	(02h),a		; hoot status変更
	jr	loop

stop:
	xor	a		; hoot status変更
	out	(02h),a

	call	090h
	jr	loop

init:
	ld	hl,00A8CH	; wait value
	ld	(0E25CH),hl

	ld	a,001H
	ld	(0E25EH),a	; sound enable

	ld	a, 0C3h
	ld	(0FD9Fh), a
	ld	(00141h), a
	ld	hl, keychk
	ld	(00142h), hl
	ld	hl, irq
	ld	(0FDA0h), hl

	ret

irq:
	in	a,(02h)		; キー入力待ち
	cp	02h
	jr	nz, irq_end

	ld	a, 01h
	ld	(flg_stop),a

irq_end:
	ret

keychk:
	ld	a, (flg_stop)
	cpl
	bit	0, a
	ret

mustbl:
	dw	5E12h		; bank 00 1E12 SE
	db	02
	dw	5E2Eh		; bank 00 1E2E SE
	db	02
	dw	8240h		; bank 01 4240 SE
	db	02
	dw	8A62h		; bank 01 4A62 OP
	db	02
	dw	8DE2h		; bank 03 8DE2 SE
	db	04
	dw	8E8Ch		; bank 03 8E8C SE
	db	04
	dw	927Eh		; bank 03 927E OP
	db	04
	dw	9761h		; bank 03 9761 OP
	db	04
	dw	9841h		; bank 03 9841 SE
	db	04
	dw	0AEDFh		; bank    AEDF SE
	db	05
	dw	9429h		; bank    13429 OP
	db	09
	dw	0ACEAh		; bank    16CEA SE
	db	0Ah

flg_stop:
	db	00
