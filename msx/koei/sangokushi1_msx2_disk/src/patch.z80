; 三國志(MSX2 DISK) (c)Koei
;
; @autor RuRuRu
; @date 2015/09/19 1st Release
;

	cseg
		org	0A000h

stack:	equ	0f000h		; スタックエリア

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

; play
	in	a,(04h)
	cp	02h
	jr	z,play_se
	call	061F3h		; Set loop flag

	call	061EDh		; Stop

	ld	a, 01h
	ld	(0635Ah), a	; Sound enable
	in	a,(03h)		; Code No.
	call	05E3FH		; Play
	jr	play_end

play_se:
	in	a,(03h)		; Code No.
	add	a, a
	ld	e, a
	ld	d, 00h
	ld	hl,setbl
	add	hl,de
	ld	e, (hl)
	inc	hl
	ld	d, (hl)
	ld	hl,00000H
	call	05CDCH		; Play SE
	jr	play_end

stop:
	xor	a
	ld	(0635Ah), a	; Sound disable
	call	061EDh

play_end:
	xor	a		; hoot status変更
	out	(02h),a
	jr	loop

init:
	ld	a, 0C3h
	ld	(001Ch), a
	ld	(0038h), a
	ld	(0093h), a
	ld	(0096h), a
	ld	(5E34h), a
	ld	hl, irq
	ld	(0039h), hl
	ld	hl, calslt
	ld	(001Dh), hl
	ld	hl, getdat
	ld	(5E35h), hl
	ld	hl, wtpsg
	ld	(0094h), hl
	ld	hl, rdpsg
	ld	(0097h), hl

	ld	a, 0C9h
	ld	(05C81h),a	; original irq

	call	05C29h		; VDP port

	ret

; 本来はVRAMから読むのを回避
getdat:
	ld	a,(hl)
	ret

irq:
	push	hl
	push	de
	push	bc
	push	af
	exx
	ex	af,af'
	push	hl
	push	de
	push	bc
	push	af
	push	iy
	push	ix
	call	05FB8h		; Sound irq
	pop	ix
	pop	iy
	pop	af
	pop	bc
	pop	de
	pop	hl
	ex	af,af'
	exx
	pop	af
	pop	bc
	pop	de
	pop	hl
	ei
	ret

calslt:
	jp	(ix)

wtpsg:
	out	(0A0h),a
	push	af
	ld	a,e
	out	(0A1h),a
	pop	af
	ret

rdpsg:
	out	(0A0h),a
	in	a,(0A1h)
	ret

setbl:
	dw	0043Bh
	dw	018EBh
	dw	01905h
