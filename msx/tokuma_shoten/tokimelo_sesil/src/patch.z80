;
; ときめきSESIL (c)Technopolice Soft
;
; @autor RuRuRu
; @date 2024/05/18 1st Release
;

	cseg
	org	08000h

stack:	equ	0f380h

main:
	di
	im	1
	ld	sp, stack
	call	init
	ei
	jp	loop

loop:
	in	a, (02h)	; Key wait
	or	a
	jr	z, loop

	cp	01h
	jr	nz, stop


	di
	in	a, (03h)
	out	(03h), a	; Load music data
	ei

	cp	00
	jr	z, open
	cp	01
	jr	z, end
	cp	02
	jr	z, secil1
	cp	03
	jr	z, secil2
	cp	04
	jr	z, select

open:
	ld	a, 0C9h
	ld	(01241h), a
	ld	(01250h), a

	call	01235h
	xor	a
	ld	(irq),a
	jr	loop

end:
	call	01800h
	xor	a
	ld	(irq),a

	jr	loop

secil1:
	in	a, (04h)
	add	a, a	; x2
	ld	e, a
	ld	d, 00h
	ld	hl, tblsecil1
	add	hl, de
	ld	e, (hl)
	inc	hl
	ld	d, (hl)
	push	de
	pop	hl
	ld	de, play_end
	push	de
	jp	(hl)	; play

secil2:
	in	a, (04h)
	add	a, a	; x2
	ld	e, a
	ld	d, 00h
	ld	hl, tblsecil2
	add	hl, de
	ld	e, (hl)
	inc	hl
	ld	d, (hl)
	push	de
	pop	hl
	ld	de, play_end
	push	de
	jp	(hl)	; play

select:
	call	01307h
	xor	a
	ld	(irq),a

	jr	loop

stop:
	ld	a, 0C9h
	ld	(irq),a
	call	090h

play_end:
	xor	a		; Change status
	out	(02h), a
	jr	loop

init:
	ld	a, 0C9h
	ld	(008BDh), a
	ld	(008BEh), a
	ld	(irq),a

	call	08A0h

	ld	a, 0C3h
	ld	(0001Ch), a
	ld	(00038h), a
	ld	(00090h), a
	ld	(00093h), a

	ld	hl, calslt
	ld	(0001Dh), hl
	ld	hl, irq
	ld	(00039h), hl
	ld	hl, psgini
	ld	(00091h), hl
	ld	hl, psgw
	ld	(00094h), hl

	ret

calslt:
	jp	(ix)

psgini:
	ld	e,00h
	ld	a,08h
	call	psgw
	inc	a
	call	psgw
	inc	a
	call	psgw
	inc	a

	ld	e,0B8h
	ld	a,07
	call	psgw
	ret

psgw:
	di
	out     (0A0h),a
	push    af
	ld      a,e
	out     (0A1h),a
	ei
	pop     af
	ret

irq:
	nop
	push	hl
	push	de
	push	bc
	push	af
	exx
	ex	af,af'
	push	hl
	push	de
	push	bc
	push	af
	push	iy
	push	ix
	di
	in	a,(099h)
	or	a
	jp	p,int_end
	call	00961h
	ei
int_end:
	pop	ix
	pop	iy
	pop	af
	pop	bc
	pop	de
	pop	hl
	ex	af,af'
	exx
	pop	af
	pop	bc
	pop	de
	pop	hl
	ei
	ret

tblsecil1:
	dw	01334h
	dw	01341h
	dw	0134Eh
	dw	0135Bh
	dw	01368h
	dw	01375h
	dw	01382h
	dw	0138Fh
	dw	0139Ch
	dw	013A9h

tblsecil2:
	dw	0138Dh
	dw	0139Ah
	dw	013A7h
	dw	013B4h
	dw	013C1h
	dw	013CEh
	dw	013DBh
	dw	013E8h
	dw	013F5h
	dw	01402h	; stop
