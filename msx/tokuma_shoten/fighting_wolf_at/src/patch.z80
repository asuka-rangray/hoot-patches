; 単騎狼 Wolf AT (c)Technopolis Soft
;
; @autor RuRuRu
; @date 2011/10/13 1st Release
;

	cseg
	org	00400h

stack:	equ	0f380h

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; Key wait
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

; play
	in	a,(03h)

	add	a, a		; x2
	add	a, a		; x4
	add	a, a		; x8
	ld	d, 0
	ld	e, a
	ld	hl, mustbl
	add	hl, de
	push	hl
	pop	de

	ld	a, (de)
	inc	de
	ld	l, a
	ld	a, (de)
	ld	h, a
	inc	de
	call	07439H		; Set ch 0 sound data
	ld	a, (de)
	inc	de
	ld	l, a
	ld	a, (de)
	ld	h, a
	inc	de
	call	0744CH		; Set ch 1 sound data
	ld	a, (de)
	inc	de
	ld	l, a
	ld	a, (de)
	ld	h, a
	inc	de
	call	07459H		; Set ch 2 sound data

	ld	a, (de)
	ld	(exe_limit), a
	ld	c, a
	xor	a
	ld	(exe_count), a

	call	04488h

	ld	a, 01h
	ld	(play_flg), a

	jr	play_end

stop:
	xor	a
	ld	(play_flg), a
	call	04488H		; PSG mask to 0BFh

play_end:
	xor	a		; Change hoot status
	out	(02h),a
	jr	loop

init:
	ld	a, 0c3h
	ld	(001Ch), a
	ld	(0038h), a
	ld	(0093h), a
	ld	hl, irq
	ld	(0039h), hl
	ld	hl, calslt
	ld	(001Dh), hl
	ld	hl, psgw
	ld	(0094h), hl

	ld	a, 0C9h
	ld	(05246h), a

	ret

calslt:
	jp	(ix)

psgw:
	di
	out     (0A0h),a
	push    af
	ld      a,e
	out     (0A1h),a
	ei
	pop     af
	ret

irq:
	push	hl
	push	de
	push	bc
	push	af
	exx
	ex	af,af'
	push	hl
	push	de
	push	bc
	push	af
	push	iy
	push	ix

	ld	hl, play_flg
	ld	a, (hl)
	cp	0
	jr	z, irq_end

	ld	a, (exe_count)
	inc	a
	ld	(exe_count), a

	ld	hl, exe_limit
	cp	(hl)
	jr	nz, irq_end

	xor	a
	ld	(exe_count), a
	call	075ABh
	call	07466h

irq_end:
	pop	ix
	pop	iy
	pop	af
	pop	bc
	pop	de
	pop	hl
	ex	af,af'
	exx
	pop	af
	pop	bc
	pop	de
	pop	hl
	ei
	ret

play_flg:
	db	00h

exe_count:
	db	00h

exe_limit:
	db	00h

mustbl:
	dw	0B000h		; AMAZON
	dw	0B200h
	dw	0B400h
	db	04, 00

	dw	0B600h		; CALIFOA
	dw	0B800h
	dw	0BA00h
	db	06, 00

	dw	0BC00h		; PROG12
	dw	0BE00h
	dw	0C000h
	db	04, 00

	dw	0C200h		; RUN5 エンディングだと思うんだけど情報求む
	dw	0C400h
	dw	0C600h
	db	04, 00

	dw	0C800h		; SAHARA
	dw	0CA00h
	dw	0CC00h
	db	04h, 00h

	dw	0CE00h		; SAMBA
	dw	0D000h
	dw	0D200h
	db	06h, 00		; ok

	dw	0D400h		; TOKYO
	dw	0D600h
	dw	0D800h
	db	04h, 00h	; ok
