; Craze (c)Heart Denshi
;
; @autor RuRuRu
; @date 2011/08/18 1st Release
;

	cseg
	org	0400h

stack:	equ	0f380h		; スタックエリア

; メインループ

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

; play
	xor	a
	call	057CBH

	in	a,(03h)		; Code No.
	add	a,a
	add	a,a
	ld	l,a
	ld	h,000H
	ld	de,mustbl
	add	hl,de

	ld	a,(hl)
	ld	(07000h), a
	inc	hl
	ld	a,(hl)
	call	setsnd

	inc	hl
	ld	e,(hl)
	inc	hl
	ld	d,(hl)
	ex	de, hl
	ld	(sndadr),hl
	call	0581EH
sndadr:	dw	0000h
	jr	play_end

stop:
	di
	xor	a
	call	057CBH
	ei

play_end:
	xor	a		; hoot status変更
	out	(02h),a
	jr	loop

init:
; Regist entry
	ld	a, 0c3h
	ld	(0FD9Fh), a
	ld	hl, 05099h
	ld	(0FDA0h), hl

	xor	a
	call	057CBH		; stop

	ret

setsnd:
	push	hl
	push	bc
	ld	hl,mustbl2
	add	a,a		; x2
	ld	b,00h
	ld	c,a
	add	hl,bc
	ld	a,(hl)
	ld	(0E674H),a
	inc	hl
	ld	a,(hl)
	ld	(0E675H),a
	pop	bc
	pop	hl
	ret

mustbl:
	db	03h, 00h
	dw	0B762h
	db	03h, 00h
	dw	0BC62h
	db	03h, 00h
	dw	0B3E2h

	db	04h, 01h
	dw	0A5C9h
	db	04h, 01h
	dw	0A9C9h

	db	05h, 02h
	dw	0A4E0h
	db	05h, 02h
	dw	0A660h

	db	06h, 03h
	dw	0A6C1h
	db	06h, 03h
	dw	0A9C1h

	db	07h, 04h
	dw	0AA50h
	db	07h, 04h
	dw	0AF50h

mustbl2:
	dw	0AF6Ah
	dw	0AAC9h
	dw	0A760h
	dw	0AAC1h
	dw	0B050h
