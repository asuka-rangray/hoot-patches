; Rune Worth Music Disk (c)1990 T&E soft
;
; @author RuRuRu
; @date 2012/05/24 1st Release

	cseg
	org	02800h

stack:	equ	0f380h		; スタックエリア
mdata:	equ	03000h		; 曲データアドレス

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei
	jp	loop

loop:
	in	a,(02h)		; Key wait
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	call	0109h		; Stop

	di
	in	a,(03h)
	out	(03h),a		; 曲データを読み込む

	xor	a
	ld	b, a
	in	a,(04h)		; Loop
	ld	hl, mdata
	call	0106h
	ei

	jr	loop

stop:
	ld	b, 05h
	call	0010Ch		; Fade out

	xor	a		; 停止処理
	out	(02h),a
	jr	loop

init:
	ld	a, 0c9h
	ld	(0024h), a
	ld	a, 0c3h
	ld	(0038h), a
	ld	hl, irq
	ld	(0039h), hl

	in	a, (07h)
	and	01h
	jr	z, dev_psg

; opll
	ld	a, 00h
	ld	(0426h), a	; Patch for OPLL check.

dev_psg:
	call	0103h

	ret

irq:
	push	hl
	push	de
	push	bc
	push	af
	exx
	ex	af,af'
	push	hl
	push	de
	push	bc
	push	af
	push	iy
	push	ix
	call	00100h
	pop	ix
	pop	iy
	pop	af
	pop	bc
	pop	de
	pop	hl
	ex	af,af'
	exx
	pop	af
	pop	bc
	pop	de
	pop	hl
	ei
	ret
