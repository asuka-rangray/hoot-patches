; Hydlide3: The Space Memories MSX2 (c)1987 T&E soft
;
; @author RuRuRu
; @date 2010/03/16 1st Release

	cseg
	org	0400h

stack:	equ	0f000h

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; Key wait
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	in	a,(03h)		; Code No.

	cp	00FH
	jp	nc,play_se

	push	af
	call	0BB06h		; Stop
	pop	af

; play
	di
	add	a,a
	ld	l,a
	ld	h,000H
	ld	de,mustbl
	add	hl,de
	ld	a,(hl)
	inc	hl
	ld	h,(hl)
	ld	l,a
	call	0BAC7h		; Play Request
	ei
	jp	play_end

play_se:
	sub	0Fh
	call	0BAD0h

play_end:
	xor	a
	out	(02h),a		; Change hoot status
	jr	loop

stop:
	xor	a		; Change hoot status
	out	(02h),a

	di
	ld	b, 08h
	call	0BAE2h		; Fade out
;	call	0BB06h		; Stop
	ei
	jr	loop

init:
; bank change
	ld	a, 00h
	ld	(0DEB8h), a
	ld	a, 29h
	ld	(0DEB9h), a
	ld	(6800h), a
	ld	a, 2Ah
	ld	(0DEBAh), a
	ld	(7000h), a
	ld	a, 0Fh
	ld	(7800h), a

; entry
	ld	a, 0C3h
	ld	(0FD9Fh), a
	ld	hl, irq
	ld	(0FDA0h), hl

; copy table
	ld	hl, 0BAAFh	; 曲テーブル
	ld	de, musend
	ld	bc, 018h
	ldir

	call	092A9h
	ret

irq:
	di
	call	092EAh
	ret

mustbl:
	dw	8235h
	dw	8BC0h
	dw	8802h
musend:
