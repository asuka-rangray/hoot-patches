; Super Laydock: Mission Striker (c)1987 T&E soft
;
; @author RuRuRu
; @date 2011/01/12 1st Release
; @date 2025/08/03 Support Boss Music
; @date 2025/08/04 Support SE

	cseg
	org	0400h

stack:	equ	0f380h

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; Key wait
	or	a
	jr	z,loop

	cp	01h
	jr	nz,play_stop

	in	a,(04h)		; Code No.
	cp	00h
	jp	nz,play_se

	call	stop

	in	a,(03h)		; Code No.
	cp	0Eh
	jr	z, play_voice
	cp	0Fh
	jr	z, play_boss
	cp	02h
	jr	c, play_oped

; main play
	ld	a, 00h
	ld	(flg_drv), a

	di
	ld	a,000H
	ld	(06000H),a

	in	a,(03h)		; Code No.
	dec	a
	ld	(0E3CAh), a	; Code 1~

	call	04B62h		; Initialize
	call	04CB5h		; Load
	ei

play_end:
	xor	a		; Change hoot status
	out	(02h),a
	jr	loop

play_stop:
	call	stop
	jr	play_end

play_oped:
	ld	a, 01h
	ld	(flg_drv), a

	di
	ld	a,000H
	ld	(06000H),a
	ld	a,01dH
	ld	(06800H),a
	ld	a,018H
	ld	(07000H),a
	ld	a,019H
	ld	(07800H),a

	call	08017h

	in	a,(03h)		; Code No.
	cp	00h
	jr	nz, play_ed
	ld	hl,0B76EH		; OP
	jr	exec
play_ed:
	ld	hl,07580H		; ED
exec:
	call	0874EH
	ei
	jr	play_end

play_voice:
	ld	a, 02h
	ld	(flg_drv), a
	ld	a,01BH
	ld	(06000H),a
	ld	a,01CH
	ld	(06800H),a
	ld	a,018H
	ld	(07000H),a
	ld	a,019H
	ld	(07800H),a

	call	08362h
	jr	play_end

play_boss:
	ld	a, 00h
	ld	(flg_drv), a

	di
	ld	a,000H
	ld	(06000H),a

	ld	a, 01h
	ld	(0E3CAh), a	; Code 1 origin

	call	04B62h		; Initialize
	call	04CB5h		; Load

	ld	hl, 09F87h
	call	043D7h
	ei
	jr	play_end

play_se:
	ld	a, (flg_drv)
	cp	00
	jr	nz,play_end

	in	a,(03h)		; Code No.
	add	a,a
	add	a,a
	ld	l,a
	ld	h,000H
	ld	de,se_tbl
	add	hl,de
	ld	a,(hl)
	inc	hl
	ld	d,(hl)
	ld	e,a
	inc	hl
	ld	c,(hl)
	inc	hl
	ld	b,(hl)
	call	04486H
	ei
	jp	play_end

stop:
	di
	ld	a, (flg_drv)
	cp	00h
	jr	nz, stop_oped
	call	044E6h		; Stop
	jr	stop_end
stop_oped:
	cp	01h
	jr	nz, stop_end
	call	0875Dh
stop_end:
	ei
	ret

init:
	ret

flg_drv:
	db	00h

se_tbl:
	dw	09E36H, 00205H
	dw	09E7AH, 00205H
	dw	09E9BH, 00201H
	dw	09E62H, 00205H
	dw	09E62H, 00209H
	dw	09E62H, 0020AH
	dw	09E8BH, 0020AH
	dw	09ED4H, 00205H
	dw	09F02H, 00205H
	dw	09F67H, 00205H
