; Replicart (c)1987 Sony
;
; @autor RuRuRu
; @date 2010/07/16 1st Release
; @date 2020/06/01 Fix stop

	cseg
	org	0400h

stack:	equ	0f380h

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; Key wait
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	in	a,(04h)
	cp	01h
	jr	z,play_se

; play
	xor	a
	ld	(0C082h),a
	in	a,(03h)		; Code No.
	ld	(0C081h),a
	call	049F3h
	ei
	jr	play_end

play_se:
	call	04356h

	ld	hl, se_end
	push	hl

	in	a,(03h)		; Code No.
	ld	l,a
	add	a,a
	add	a,l	; x3
	ld	l,a
	ld	h,060H
	jp	(hl)
se_end:
	call	04300h
	jr	play_end

play_stop:
	xor	a
	ld	(0C082h),a
	ld	a,01h
	ld	(0C081h),a
	call	049F3h
	ret

stop:
	call	play_stop

play_end:
	xor	a		; Change hoot status
	out	(02h),a
	jr	loop

init:
	ld	a, 0c3h
	ld	(0FD9Fh), a
	ld	hl, irq
	ld	(0FDA0h), hl

	call	play_stop

	ret

irq:
	di
	call	049E9h
	ret
