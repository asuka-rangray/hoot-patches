; Gall Force: Eternal Story 創世の序曲 (c)1986 Sony
;
; @autor RuRuRu
; @date 2011/16/13 1st Release

	cseg
	org	0100h

stack:	equ	0f380h

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; Key wait
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

; play
	call	02AC5h
	ld	hl, play_end
	push	hl

	in	a,(03h)		; Code No.
	add	a,a
	ld	l,a
	ld	h,000H
	ld	de,mustbl
	add	hl,de
	ld	a,(hl)
	inc	hl
	ld	h,(hl)
	ld	l,a
	jp	(hl)

stop:
	di
	call	02AC5h
	ei

play_end:
	xor	a		; hoot status変更
	out	(02h),a
	jr	loop

init:
	ld	a, 0c9h
	ld	(02D6Dh), a
	ld	a, 0c3h
	ld	(0038h), a
	ld	hl, irq
	ld	(00039h), hl
	ret

irq:
	push	hl
	push	de
	push	bc
	push	af
	exx
	ex	af,af'
	push	hl
	push	de
	push	bc
	push	af
	push	iy
	push	ix
	call	02D49h
	pop	ix
	pop	iy
	pop	af
	pop	bc
	pop	de
	pop	hl
	ex	af,af'
	exx
	pop	af
	pop	bc
	pop	de
	pop	hl
	ei
	ret

mustbl:
	dw	02EBh		; BGM
	dw	02FCh		; OPENING
	dw	030Fh		; ENDING
	dw	037Bh		; OPENING
