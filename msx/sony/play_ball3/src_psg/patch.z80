; Play Ball3 PSG (c)1989 Sony
;
; @autor RuRuRu
; @date 2011/08/19 1st Release

	cseg
	org	0400h

stack:	equ	0f380h

main:
	di
	im	1
	ld	sp,stack
	ei

loop:
	in	a,(02h)		; Key wait
	or	a
	jr	z,loop

	cp	01h
	jr	nz, play_stop

; play
	call	stop
	call	setdrv

	ld	hl, play_end
	push	hl
	ld	hl, (fnc_play)
	in	a,(03h)		; Code No.
	jp	(hl)

stop:
	ld	hl,(fnc_stop)
	jp	(hl)

play_stop:
	call	stop

play_end:
	xor	a		; Change hoot status
	out	(02h),a
	jr	loop

play0:
	; PSG.BIN
	inc	a
	ld	(0C904h), a
	call	0A003h
;	call	0A006h	; SE
;	call	0A009h	; SE
	ret
play1:
	; ENDPSG.BIN
	inc	a
	ld	(0C901h), a
	call	0A933h
;	call	0A936h	; SE
;	call	0A939h	; SE
	ret
play2:
	; FM0.BIN
	push	af
	in	a, (05h)
	out	(03h),a		; load data
	add	a,a		; x2
	ld	hl, mustbl
	ld	b,000H
	ld	c,a
	add	hl,bc
	ld	e,(hl)
	inc	hl
	ld	d,(hl)
	pop	af

	add	a,a		; x2
	ld	l,a
	ld	h,00h
	add	hl,de

	ld	e,(hl)
	inc	hl
	ld	d,(hl)
	ex	de,hl

	jp	(hl)

stop0:
	; PSG.BIN
	xor	a
	ld	(0C904h), a
	call	0A003h
	ret
stop1:
	; ENDPSG.BIN
	xor	a
	ld	(0C901h), a
	call	0A933h
	ret
stop2:
	; FM0.BIN
	call	0B460h
;	call	0B5B8h
	ret

dummy:
	ret

setdrv:
	ld	a, (flg_drv)
	ld	b,a
	in	a, (04h)
	ld	(flg_drv), a
	cp	b
	ret	z

	di

	ld	hl,drvtbl
	add	a,a		; x2
	add	a,a		; x4
	ld	e,a
	add	a,a		; x8
	add	a,e		; x12
	ld	b,000H
	ld	c,a
	add	hl,bc

	ld	e,(hl)
	inc	hl
	ld	d,(hl)
	inc	hl
	ld	(0FDA0h), de

	ld	e,(hl)
	inc	hl
	ld	d,(hl)
	inc	hl
	push	de
	ld	e,(hl)
	inc	hl
	ld	d,(hl)
	inc	hl
	ld	c,(hl)
	inc	hl
	ld	b,(hl)
	inc	hl
	push	de
	push	bc

	ld	c,(hl)
	inc	hl
	ld	b,(hl)
	inc	hl
	ld	(fnc_play), bc

	ld	c,(hl)
	inc	hl
	ld	b,(hl)
	inc	hl
	ld	(fnc_stop), bc

	pop	bc
	pop	de
	pop	hl
	ldir

	ld	a, 0C3h
	ld	(0FD9Fh), a

	xor	a
	ld	hl,0C000h
	ld	de,0C001h
	ld	bc,02000h
	ld	(hl),a
	ldir


	ei
	ret

fnc_play:
	dw	dummy
fnc_stop:
	dw	dummy

flg_drv:
	db	04h

drvtbl:
	; PSG.BIN
	dw	0A000h	; IRQ
	dw	03500h	;
	dw	0A000h	; Address
	dw	02000h	; Size
	dw	play0
	dw	stop0

	; ENDPSG.BIN
	dw	0A930h
	dw	06000h
	dw	0A930h
	dw	02000h
	dw	play1
	dw	stop1

	; FM0.BIN
	dw	0AC40h
	dw	08000h
	dw	0AB80h
	dw	02500h
	dw	play2
	dw	stop2

mustbl:
	dw	mustbl0
	dw	mustbl1
	dw	mustbl2
	dw	mustbl3
	dw	mustbl4
	dw	mustbl5

mustbl0:
	dw	0B6C0h
	dw	0B6FEh
	dw	0B72Dh
	dw	0B7A2h
	dw	0B7DDh
	dw	0B81Dh
	dw	0B861h
	dw	0B8B5h
	dw	0B8EDh
	dw	0B917h
	dw	0B9B8h
	dw	0BA00h
	dw	0BA3Eh
	dw	0BA84h
	dw	0BADAh
	dw	0BB19h
	dw	0BB79h

mustbl1:
	dw	0C200h
	dw	0C53Dh
	dw	0C901h
	dw	0CD15h

mustbl2:
	dw	0C200h
	dw	0C618h
	dw	0CA45h
	dw	0CD04h

mustbl3:
	dw	0C200h
	dw	0CA3Fh
	dw	0CE6Ch

mustbl4:
	dw	0C200h
	dw	0C53Dh
	dw	0C901h

mustbl5:
	dw	0C200h
	dw	0C53Dh
