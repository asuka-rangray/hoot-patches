; Alpha Squadron (c)Sony 1984
;
; @autor RuRuRu
; @date 2012/05/15 1st Release
; @date 2020/11/25 Support S.E.
; @date 2020/11/26 Fix stop
;

	cseg
	org	0400h

stack:	equ	0f300h

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; Key wait
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	in	a,(04h)		; flag
	cp	01h
	jr	z,play_se

; play
	di
	call	04242h		; Work Init
	ei

	in	a, (03h)
	ld	(0E0EDh),a	; Start ptr
	ld	a,0FFH
	ld	(0E0A9H),a	; Length
	ld	a,00DH
	ld	(0E189H),a	; Volume
	ld	a,00AH
	ld	(0E0B7H),a	; Tempo
	call	04142h
	jr	play_end

play_se:
	call	06EA0h
	in	a, (03h)
	ld	b, a
	add	a, a
	add	a, b
	ld	l, a
	ld	h, 00h
	ld	de, setbl
	add	hl, de
	ld	e, (hl)
	inc	hl
	ld	d, (hl)
	inc	hl
	ld	a, (hl)
	push	de
	pop	hl
	ld	b, a
	call	052C2H		; PSG array write
	jr	play_end

stop:
	xor	a
	ld	(0E0ACh),a	; Tempo 0
	call	06EA0h

play_end:
	xor	a		; Change hoot status
	out	(02h),a
	jr	loop

init:
	ld	a, 0C3h
	ld	(0FD9Fh), a
	ld	hl, irq
	ld	(0FDA0h), hl

	call	06E82h		; PSG init
	call	06EA0h		; PSG mute

	ret

irq:
	di
	call	0442Bh
	ret

setbl:
	dw	07190H
	db	00DH
	dw	07172H
	db	003H
	dw	071B2H
	db	005H
	dw	05B90H
	db	005H
	dw	071AAH
	db	004H
	dw	07180H
	db	003H
	dw	07178H
	db	004H
	dw	07186H
	db	005H
	dw	052D0H
	db	008H
