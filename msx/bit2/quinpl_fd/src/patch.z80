; QUINPL (c)Bit2 用
; メインルーチン

	cseg
	org	0400h

stack:	equ	0f380h		; スタックエリア

; メインループ

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

; play
	in	a,(04h)
	cp	0FFh
	jr	z,play_se

	call	play_stop

	di
	in	a,(03h)		; Code No.
	out	(03h),a
	ei

	in	a,(04h)		; Sub Code
	inc	a		; 1 origin
	ld	(0BDD2H),a	; Sound Code
	ld	(flgno), a

	in	a,(05h)		; Loop flag
	ld	(flgloop), a

	jr	loop

play_se:
	in	a,(03h)		; Code No.
	ld	(0BDD0h), a
	jr	play_end

stop:
	xor	a
	ld	(flgloop), a
	call	057F4h

play_end:
	xor	a		; hoot status変更
	out	(02h),a
	jr	loop

play_stop:
	call	057F4h
	ret

init:
	ld	a, 0c3h
	ld	(0FD9Fh), a
	ld	hl, irq
	ld	(0FDA0h), hl

	ld	a, 0C9h
	ld	(0A400h),a

	in	a,(07h)
	ld	(0BC0Ch), a

	call	057F4h
	ret

irq:
	call	0A400H		; Sound irq
	ld	a, (flgloop)
	cp	01h
	jr	nz, irq_end
	ld	a, (flgno)
	ld	(0BDD2h),a
irq_end:
	ret

flgloop:
	db	00h
flgno:
	db	01h
