; 秘録 首斬り館 (c)Bit2 用
; メインルーチン

	cseg
	org	0400h

stack:	equ	0f380h		; スタックエリア

; メインループ

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

; play
	call	play_stop

	in	a,(07h)
	cp	00h
	call	z,patch_psgdrv

	di
	in	a,(03h)		; Code No.
	out	(03h),a
	ei

	in	a,(04h)		; Sub Code
	inc	a		; 1 origin
	ld	(0CE02H),a	; Sound Code
	ld	(flgno), a

	in	a,(05h)		; Loop flag
	ld	(flgloop), a

	jr	loop


stop:
	xor	a
	ld	(flgloop), a
	ld	a, 01h
	ld	(0CEFDh), a	; Fade out

play_end:
	xor	a		; hoot status変更
	out	(02h),a
	jr	loop

play_stop:
	in	a,(07h)
	cp	00
	jr	z,stop_psg
	call	085E0h
	ret
stop_psg:
	call	089D0h
	ret

patch_psgdrv:
	ld	hl,08FFAh			; for 9000h
	in	a,(03h)
	cp	03h
	jr	nz,_patch_end
;	ld	hl,08FFAh			; for 9000h
	ld	hl,08C7Ah			; for 8C00h
_patch_end:
	ld	(0896BH),hl
	ret

init:
	ld	a, 0c3h
	ld	(0FD9Fh), a
	ld	hl, irq
	ld	(0FDA0h), hl

	in	a,(07h)
	cp	00
	jr	z, psg_init

	call	08018h		; OPLL init
	ret

psg_init:
	ld	a,0FFh
	ld	(0CC01h),a
	ret

irq:
	call	08000H		; Sound irq
	ld	a, (flgloop)
	cp	01h
	jr	nz, irq_end
	ld	a, (flgno)
	ld	(0CE02h),a
irq_end:
	ret

flgloop:
	db	00h
flgno:
	db	01h
