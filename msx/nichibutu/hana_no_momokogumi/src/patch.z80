; 花のももこ組! (c)Nichibutu
;
; @autor RuRuRu
; @date 2012/11/28 1st Release

	cseg
	org	01000h

stack:	equ	0f380h		; スタックエリア

; メインループ

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

; play
	di
	in	a,(03h)		; Code No.
	call	0897Eh
	ei
	jr	play_end

stop:
	di
	ld	a, 0FFh
	call	0897Eh
	ei

play_end:
	xor	a		; hoot status変更
	out	(02h),a
	jr	loop

init:
	ld	a, 0c9h
	ld	(0024h), a
	ld	a, 0c3h
	ld	(001Ch), a
	ld	(0038h), a
	ld	(0093h), a
	ld	(0096h), a
	ld	(80D4h), a
	ld	hl, calslt
	ld	(0001Dh), hl
	ld	hl, irq
	ld	(00039h), hl
	ld	hl, psgw
	ld	(00094h), hl
	ld	hl, psgr
	ld	(00097h), hl
	ld	hl, fmbios
	ld	(080D5h), hl

	in	a, (07h)
	cp	01h
	jr	nz, psgdev

	ld	(08E66h), a	; opll flag
	call	08056h

psgdev:
	ld	a, 0FFh
	call	0897Eh

	ret

calslt:
	jp	(ix)

fmbios:
	di
	jp	(ix)

psgw:
	di
	out     (0A0h),a
	push    af
	ld      a,e
	out     (0A1h),a
	ei
	pop     af
	ret

psgr:
	out     (0A0h),a
	in      a,(0A2h)
	ret

irq:
	push	hl
	push	de
	push	bc
	push	af
	exx
	ex	af,af'
	push	hl
	push	de
	push	bc
	push	af
	push	iy
	push	ix
	call	083EBh
	pop	ix
	pop	iy
	pop	af
	pop	bc
	pop	de
	pop	hl
	ex	af,af'
	exx
	pop	af
	pop	bc
	pop	de
	pop	hl
	ei
	ret
