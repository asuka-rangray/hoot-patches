; 極道陣取り (c)MicroNet
;
; @autor RuRuRu
; @date 2011/04/15 1st Release
; @date 2023/03/16 Add SDAT.COM support
;

	cseg
	org	0400h

stack:	equ	0f380h		; Stack

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; Key wait
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

; play
	call	play_stop

	in	a,(03h)
	push	af

	in	a,(04h)
	ld	(drvno), a
	out	(03h),a
	cp	01h
	jr	z,endmsx
	cp	02h
	jr	z,sdat

;title
	ld	hl, 7000h
	ld	de, 7C00h
	ld	bc, 3000h
	lddr

	ld	de,mustbl
	ld	ix,05B20h		; title.com:req
	jr	playreq

endmsx:
	ld	de,mustbl2
	ld	ix,05296h		; endmsx.com:req
	jr	playreq

sdat:
	ld	a, 01h
	out	(03h),a
	pop	af
	ld	de,mustbl3
	add	a,a		; x2
	ld	l,a
	ld	h,000H
	add	hl,de
	ld	e,(hl)
	inc	hl
	ld	d,(hl)
	push	de
	pop	hl
	ld	de,0BD12h
	ld	bc,0310h
	ldir
	ld	hl ,0BD12h
	call	05296h		; endmsx.com:req
	jr	play_end

playreq:
	pop	af
	ld	hl, play_end
	push	hl
	add	a,a		; x2
	ld	l,a
	ld	h,000H
	add	hl,de
	ld	e,(hl)
	inc	hl
	ld	d,(hl)
	ex	de,hl
	jp	(ix)


play_stop:
	ld	a,(drvno)
	cp	00h
	jr	nz,endmsx_stop

	call	05B75h
	ret

endmsx_stop:
	call	052EBh
	ret

stop:
	call	play_stop

play_end:
	xor	a		; hoot status変更
	out	(02h),a
	jp	loop

init:
	in	a, (07h)
	ld	(0C01Dh), a	; opll flag

	ld	a, 0C9h		; 演奏前にPで停止した時の保険
	ld	(05B75h), a
	ret

mustbl:
	dw	072B4h
	dw	06C31h
	dw	0623Bh
	dw	06749h

mustbl2:
	dw	059BEh
	dw	06245h
	dw	07147h

mustbl3:
	dw	01000h
	dw	01252h
	dw	014F8h

drvno:
	db	00h
