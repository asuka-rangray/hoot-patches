; 琥珀色の遺言(OPLL) (c)Riverhill Soft
;
; @autor RuRuRu
; @date 2012/01/09 1st Release
;

	cseg
	org	0400h

stack:	equ	0f380h		; スタックエリア

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	call	chk_loop

	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

; play
	call	play_stop

	in	a,(04h)
	cp	02
	jr	z, with_intro

	push	af
	in	a,(03h)		; Code No.
	out	(03h),a
	pop	af

	in	a,(04h)
	and	1		; loop (0 or 1)

	ld	hl, 0A000h
	di
	call	04116h		; MSTART
	ei
	jr	loop

with_intro:
	in	a,(03h)		; Code No.
	push	af
	out	(03h),a

	ld	hl, 0A000h
	ld	de, 09000h
	ld	bc, 01000h
	ldir

	pop	af
	inc	a
	out	(03h),a

	ld	a, 01
	ld	hl, 09000h
	call	04116h		; MSTART

	ld	a, 1
	ld	(flg_loop), a
	jr	loop

play_stop:
	di
	xor	a
	ld	(flg_loop),a
	call	04119h		; MSTOP
	ei
	ret

stop:
	call	play_stop

play_end:
	xor	a
	out	(02h),a		; hoot status変更
	jr	loop

init:
	ld	a, 0c3h
	ld	(0FD9Fh), a
	ld	hl, 0411Fh
	ld	(0FDA0h), hl

	ld	a, 0c9h
	ld	(0FD9Ah), a

	ld	hl,03000H
	call	04113H		; INIOPL

	ret

chk_loop:
	ld	a, (flg_loop)
	cp	00
	jr	z, chk_end

	call	04122h
	ei
	and	a
	jr	nz,chk_end

	call	play_stop

	ld	hl, 0A000h
	xor	a		; Loop Limit
	ld	(flg_loop),a
	call	04116h
chk_end:
	ret

flg_loop:
	db	00h
