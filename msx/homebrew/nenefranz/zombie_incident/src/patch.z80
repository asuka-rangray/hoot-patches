; Zombie Incident Ve1.2 (c)nenefranz
; CRC32(0x0d5c497a)
;
; @autor RuRuRu
; @date 2020/11/10 1st Release
;

	cseg
	org	0e500h

stack:	equ	0c100h		; Stack

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; Key wait
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	in	a,(04h)
	cp	01h
	jr	z,play_se
	cp	02h
	jr	z,set_speed

	call	play_stop

	in	a,(03h)		; Code No.
	call	072E2h		; BGM play

	jr	play_end

play_se:
	ld	hl, play_end
	push	hl

	in	a,(03h)		; Code No.
	add	a,a
	ld	l,a
	ld	h,000H
	ld	de,setbl
	add	hl,de
	ld	a,(hl)
	inc	hl
	ld	h,(hl)
	ld	l,a
	jp	(hl)

play_stop:
	xor	a
	call	072E2h		; BGM play
	ret

set_speed:
	ld	a, (0C102h)
	xor	0Eh		; 32h or 3Ch
	ld	(0C102h),a
	jr	play_end

stop:
	call	play_stop

play_end:
	xor	a		; Change hoot status
	out	(02h),a
	jr	loop

init:
	call	04061h		; Slot init
	call	04384h		; Sound init
	call	04225h		; Sound stop

	ld	hl,0ACEAh	;
	call	04394H		; Set data

	ld	a,03Ch
	ld	(0C102h),a	; Play speed

	call	042EEh		; Enable IRQ
	ret

setbl:
	dw	7BB3h		; 00
	dw	7BBEh
	dw	7BC9h
	dw	7BD4h
	dw	7BDFh
	dw	7BEAh
	dw	7BF5h
	dw	7C00h
	dw	7C0Bh
	dw	7C16h		; 09
