; hoot patch for 魔導物語1-2-3 (c)Compile
;
; @autor RuRuRu
; @date 2012/12/04 1st Release
;

	cseg
	org	0f000h

stack:	equ	0f480h		; スタックエリア

; メインループ

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

; play

	in	a, (03h)	; Code No.
	push	af

	di
	call	0124h		; Stop
	in	a,(04h)
	out	(03h),a
	add	a,a
	ld	l,a
	ld	h,000H
	ld	de,mustbl
	add	hl,de
	ld	a,(hl)
	inc	hl
	ld	h,(hl)
	ld	l,a
	push	hl
	push	hl
	pop	de
	ld	hl, 0C000h
	ld	bc, 02000h
	ldir

	in	a, (05h)
	cp	0
	call	nz,012Ah
	pop	hl

	ld	a, 03h		; Set data
	call	012Ah
	ei

	pop	af
	call	012Dh		; Play

	jr	play_end

stop:
	call	0124h		; Stop

play_end:
	xor	a		; hoot status変更
	out	(02h), a
	jr	loop

init:
; Regist entry
	ld	a, 0c3h
	ld	(0038h), a
	ld	(001Ch), a

	ld	hl, timer
	ld	(0039h), hl

	ld	hl, calslt
	ld	(001Dh), hl

; Init
;	ld	a, 0C0h
;	ld	a, 080h			; M-MUS
	xor	a			; PSG
	call	012Ah

	ld	a, 01h
	call	012Ah

	ret

calslt:
	ret
;	jp	(ix)

timer:
	push	hl
	push	de
	push	bc
	push	af
	exx
	ex	af,af'
	push	hl
	push	de
	push	bc
	push	af
	push	iy
	push	ix
	call	00121H
	pop	ix
	pop	iy
	pop	af
	pop	bc
	pop	de
	pop	hl
	ex	af,af'
	exx
	pop	af
	pop	bc
	pop	de
	pop	hl
	ei
	ret

fmflag:
	db	00h

mustbl:
	dw	05EA8h		; table (1-op)
	dw	02002h		; table (1/2/3-main)
	dw	02002h		; table (1/2/3-main)
	dw	02002h		; table (1/2/3-main)
	dw	0594Bh		; table (1-ed)

	dw	06292h		; table (2-op)
	dw	02002h		; table (1/2/3-main)
	dw	02002h		; table (1/2/3-main)
	dw	02002h		; table (1/2/3-main)
	dw	02002h		; table (1/2/3-main)
	dw	0559fh		; table (2-ed)

	dw	05C90h		; table (3-op)
	dw	02002h		; table (1/2/3-main)
	dw	02002h		; table (1/2/3-main)
	dw	02002h		; table (1/2/3-main)
	dw	02002h		; table (1/2/3-main)
	dw	055BDh		; table (3-ed)
