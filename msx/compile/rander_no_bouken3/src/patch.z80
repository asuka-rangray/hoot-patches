; ランダーの冒険III (c)Compile
;
; @autor RuRuRu
; @date 2012/12/01 1st Release
;

	cseg
	org	06000h

stack:	equ	0f300h

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; Wait for event
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

; play
	in	a,(03h)		; Code No.

	di
	push	af
	in	a,(04h)
	out	(03h), a

	ld	a, (fm_flag)
	call	0C91h
	pop	af
	ei

	call	0D30h		; play

	jr	play_end

stop:
	di
	call	0CFCh
	ei

play_end:
	xor	a		; Change status of hoot
	out	(02h),a
	jr	loop

init:
; Regist entry
	ld	a, 0c3h
	ld	(0038h), a
	ld	(001Ch), a

	ld	hl, irq
	ld	(0039h), hl

	ld	hl, calslt
	ld	(001Dh), hl

; Init
	xor	a
	call	0C91h

	in	a, (07h)
	cp	00
	jr	z, dev_psg
	ld	a, 080h
	jr	init_end
dev_psg:
	xor	a
init_end:
	ld	(fm_flag), a
	call	0C91h
	ret

fm_flag:
	db	00h

calslt:
	ret

irq:
	push	hl
	push	de
	push	bc
	push	af
	exx
	ex	af,af'
	push	hl
	push	de
	push	bc
	push	af
	push	iy
	push	ix
	call	00100h
	pop	ix
	pop	iy
	pop	af
	pop	bc
	pop	de
	pop	hl
	ex	af,af'
	exx
	pop	af
	pop	bc
	pop	de
	pop	hl
	ei
	ret
