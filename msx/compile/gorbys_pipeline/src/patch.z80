; ゴルビーのパイプライン大作戦 (c)Compile
;
; @autor RuRuRu
; @date 2010/06/10 1st Release
;

	cseg
	org	06000h

stack:	equ	0f000h

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; Wait for event
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	in	a,(04h)		; flag
	cp	01h
	jr	nz,play

	call	0190Ch		; Stop

play:
	in	a,(03h)		; Code No.

	di
	call	01948h		; Play
	ei

	jr	play_end

stop:
	di
	call	0190Ch		; Stop
	ei

play_end:
	xor	a		; Change status of hoot
	out	(02h),a
	jr	loop

init:
; Regist entry
	ld	a, 0c3h
	ld	(0038h), a
	ld	hl, timer
	ld	(0039h), hl

; Init
	in	a,(07h)		; Get device info(01:opll)
	and	01h
	jp	z,devpsg
	ld	a, 80h
	jp	initdrv
devpsg:
	ld	a, 00h
initdrv:
	call	01802h

	ld	a, 01h
	call	01802h

	ld	a, 03h
	ld	hl,08000h		; data address
	call	01802h
	ret

timer:
	push	hl
	push	de
	push	bc
	push	af
	exx
	ex	af,af'
	push	hl
	push	de
	push	bc
	push	af
	push	iy
	push	ix
	call	00A1BH
	pop	ix
	pop	iy
	pop	af
	pop	bc
	pop	de
	pop	hl
	ex	af,af'
	exx
	pop	af
	pop	bc
	pop	de
	pop	hl
	ei
	ret
