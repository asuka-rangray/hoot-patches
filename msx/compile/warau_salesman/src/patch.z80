; 笑ウせぇるすまん (c)Compile
;
; @autor RuRuRu
; @date 2014/03/15 1st Release
;

	cseg
	org	0f000h

stack:	equ	0f400h

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; Wait for event
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

; play
	in	a,(04h)
	cp	1
	call	z,0124h		; Stop

	in	a,(03h)		; Code No.

	di
	call	012Dh		; Play
	ei

	jr	play_end

stop:
	di
	call	0124h
	ei

play_end:
	xor	a		; Change status of hoot
	out	(02h),a
	jr	loop

init:
; Regist entry
	ld	a, 0c3h
	ld	(0038h), a
	ld	(001Ch), a

	ld	hl, irq
	ld	(0039h), hl

	ld	hl, calslt
	ld	(001Dh), hl

; Init
	in	a, (07h)		; Device information
	cp	00
	jr	z, dev_psg
	cp	02
	jr	z, dev_audio
	ld	a, 080h			; RHYTHM:80 FM9:C0
	jr	init_end
dev_audio:
	ld	a, 0a0h
	jr	init_end
dev_psg:
	xor	a
init_end:
	call	012Ah

	ld	a, 03h
	ld	hl, 02000h
	call	012Ah
	ret

calslt:
	ret
;	jp	(ix)

irq:
	push	hl
	push	de
	push	bc
	push	af
	exx
	ex	af,af'
	push	hl
	push	de
	push	bc
	push	af
	push	iy
	push	ix
	call	0121h
	pop	ix
	pop	iy
	pop	af
	pop	bc
	pop	de
	pop	hl
	ex	af,af'
	exx
	pop	af
	pop	bc
	pop	de
	pop	hl
	ei
	ret
