; Ultima I (c)Ponyca
;
; @autor RuRuRu
; @date 2012/02/29 1st Release
;

	cseg
	org	00400h

stack:	equ	0f380h
mdata:	equ	0b000h		; Music data address

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei
	jp	loop

loop:
	in	a,(02h)		; Key wait
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	call	09700h		; stop

	di
	in	a,(03h)
	out	(03h),a		; Lead data
	ei

	ld	a, 0FEh
	call	09694h

	call	09706h		; play
	ei

	jr	loop

stop:
	call	09700h		; stop

	xor	a		; Change hoot status
	out	(02h),a
	jr	loop

init:
	ld	a, 0c9h
	ld	(0FD9Fh), a

	ld	a, 0c3h
	ld	(001Ch), a
	ld	(0038h), a
	ld	(0093h), a
	ld	(0096h), a

	ld	hl, calslt
	ld	(001Dh), hl
	ld	hl, timer
	ld	(0039h), hl
	ld	hl, psgw
	ld	(0094h), hl
	ld	hl, psgr
	ld	(0097h), hl

	ld	hl, mdata
	call	09668h		; Set address

	ret

calslt:
	jp	(ix)

psgw:
	di
	out     (0A0h),a
	push    af
	ld      a,e
	out     (0A1h),a
	ei
	pop     af
	ret

psgr:
	out     (0A0h),a
	in	a,(0A1h)
	ret

timer:
	push	hl
	push	de
	push	bc
	push	af
	exx
	ex	af,af'
	push	hl
	push	de
	push	bc
	push	af
	push	iy
	push	ix
	call	0FD9Fh
	pop	ix
	pop	iy
	pop	af
	pop	bc
	pop	de
	pop	hl
	ex	af,af'
	exx
	pop	af
	pop	bc
	pop	de
	pop	hl
	ei
	ret
