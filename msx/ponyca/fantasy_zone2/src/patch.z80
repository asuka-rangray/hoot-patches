; Fantasy Zone II The tears of OPA OPA (c)Ponyca
;
; @autor RuRuRu
; @date 2010/03/19 1st Release
;

	cseg
	org	0400h

stack:	equ	0f380h		; Stack

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; Key wait
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop


	in	a,(04h)
	cp	01h
	jr	z,play_se

; play
	in	a,(03h)		; Code No.

	ld	hl, mustbl
	add	a,a
	add	a,a
	add	a, l
	ld	l, a
	ld	e, (hl)
	inc	hl
	ld	d, (hl)
	inc	hl
	ld	a, (hl)
	ld	(077F8H),a	; 8000~9FFF
	ex	de, hl

	di
	call	079B9h
	ei
	jr	play_end

play_se:
	in	a,(03h)		; Code No.
	ld	(0E3D6h), a	; SE code
	jr	play_end

stop:
	di
	ld	hl, 0DF22h
	call	079B9h
	ei

play_end:
	xor	a		; Change hoot status
	out	(02h),a
	jr	loop

init:
	ld	a, 0c3h
	ld	(0FD9Fh), a
	ld	hl, irq
	ld	(0FDA0h), hl

	call 	044D6h

	ld	a,00AH
	ld	(0E3D6H),a	; S.E. Code

	ret

irq:
	call	07A8Bh		; Sound irq
	ret

mustbl:
	dw	0BA50h	; Title
	db	06,00
;	dw	0DF22h	; Mute?
;	db	01,00
	dw	087E1h	; Stage 1
	db	01,00
	dw	087E1h	; Stage 2
	db	02,00
	dw	087E1h	; Stage 3
	db	03,00
	dw	087E1h	; Stage 4
	db	04,00
	dw	087E1h	; Stage 5
	db	05,00
	dw	087E1h	; Stage 6
	db	06,00
	dw	087E1h	; Stage 7
	db	07,00
	dw	0D346h	; Shop
	db	01,00
	dw	0D718H	; Boss
	db	01,00
	dw	0DD90H	; Stage Clear
	db	01,00
	dw	0DE78H	; Yarare
	db	01,00
	dw	0DCFEH	; Game Over
	db	01,00
	dw	08004H	; Final Boss
	db	08,00
	dw	0800Ah	; Ending
	db	08,00
