; DOME (c)System Sacom
;
; @autor RuRuRu
; @date 2010/04/19 1st Release
;

	cseg
	org	04000h

stack:	equ	0f380h		; スタックエリア
mdata:	equ	02EF6h		; 曲データアドレス

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei
	jp	loop

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	call	00128h

	di
	in	a,(03h)
	out	(03h),a		; 曲データを読み込む
	ei

	ld	a,0FFh
	call	0012Bh

	ld	iy, mdata
	call	00125h

	jr	loop

stop:
	call	00128h

	xor	a		; 停止処理
	out	(02h),a
	jr	loop

init:
	ld	a, 0c3h
	ld	(0038h), a
	ld	(001Ch), a
	ld	(0093h), a

	ld	hl, timer
	ld	(0039h), hl

	ld	hl, calslt
	ld	(001Dh), hl

	ld	hl, wrtpsg
	ld	(0094h), hl

	call	01E7Bh
	call	01EEAh

	ret

calslt:
	jp	(ix)

wrtpsg:
	di
	out     (0A0h),a
	push    af
	ld      a,e
	out     (0A1h),a
	ei
	pop     af
	ret

timer:
	push	hl
	push	de
	push	bc
	push	af
	exx
	ex	af,af'
	push	hl
	push	de
	push	bc
	push	af
	push	iy
	push	ix
	call	01F01h
	pop	ix
	pop	iy
	pop	af
	pop	bc
	pop	de
	pop	hl
	ex	af,af'
	exx
	pop	af
	pop	bc
	pop	de
	pop	hl
	ei
	ret
