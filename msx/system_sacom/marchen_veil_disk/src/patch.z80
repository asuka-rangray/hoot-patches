; Märchen Veil I (メルヘンヴェール) for MSX2 (c)System Sacom
;
; @autor RuRuRu
; @date 2011/01/12 1st Release
; @date 2021/02/11 Support sound effect
;

	cseg
	org	00400h

stack:	equ	0f380h
mdata:	equ	04A00h		; Music data address (MAIN.BIN)

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei
	jp	loop

loop:
	call	chk_loop
	in	a,(02h)		; Waiting for input
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

;======================================
; Play request
;======================================
	in	a,(03h)
	cp	80h
	jr	nc,play_se

	call	stop_drv	; Stop (prev driver)

	in	a,(04h)
	ld	(flgdrv),a

	call	stop_drv	; Stop & Initialize (current driver)

	push	af
	ld	a, 01h
	ld	(flgplay),a
	pop	af

	cp	00h
	jr	nz,play_main

	call	play_vis
	jr	play_end

;======================================
; MAIN.BIN: Play request
;======================================

play_main:
	call	0944Ah		; Initialzie
	di
	in	a,(03h)
	out	(03h),a		; Load music data
	ei
	call	08F0Dh
	jr	loop

;======================================
; MAIN.BIN: Play request SE
;======================================
play_se:
	ld	a,(flgdrv)
	cp	01
	jr	nz, loop

	in	a,(03h)
	call	0C5B9h
	jr	play_end


;======================================
; Stop
;======================================
stop:
	xor	a
	ld	(flgplay),a

	call	stop_drv

play_end:
	xor	a		; Change status
	out	(02h),a
	jr	loop

;======================================
; VISSTG.BIN: Initialize
;======================================
init:
	call	07EB8h		; Initialize

	ld	a, 0C9h
	ld	(06B59h), a
	ld	(074D2h), a

	ret

;======================================
; VISSTG.BIN: Play request
;======================================
play_vis:
	in	a,(05h)
	ld	(flgloop),a
	in	a,(03h)
	ld	hl,mustbl
	add	a,a
	add	a,l
	ld	l,a
	ld	e,(hl)
	inc	hl
	ld	d,(hl)
	push	de
	pop	ix
	ld	hl,vis_end
	push	hl
	jp	(ix)
vis_end:
	ret

;======================================
; VISSTG.BIN: Check loop
;======================================
chk_loop:
	ld	a,(flgplay)
	cp	0
	jr	z,chk_end
	ld	a,(flgdrv)
	cp	1
	jr	z, chk_main

	ld	a,(flgloop)
	cp	1
	jr	nz,chk_end

	call	07FD7h
	and	07h
	or	a
	call	z,play_vis

chk_end:
	ret

chk_main:
	call	08F1Fh
	ret

;======================================
; Driver release
;======================================
stop_drv:
	push	af

	ld	a,(flgdrv)
	cp	0
	jr	nz, mainstop

;======================================
; VISSTG.BIN: stop
;======================================
	call	07F06h
	call	07EB8h		; Stop

	ld	a, 07h
	call	07FA2h
	jp	stopend

;======================================
; MAIN.BIN: stop
;======================================
mainstop:
	call	08F13h		; Stop

	ld	a, 07h
	call	08F10h

stopend:
	pop	af
	ret

flgdrv:
	db	00h		; 00:vistg / 01:main
flgplay:
	db	00h
flgloop:
	db	01h

mustbl:
	dw	06B34h
	dw	06D00h
	dw	074ADh
