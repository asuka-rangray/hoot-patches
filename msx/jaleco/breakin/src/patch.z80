; Break In (c)Jaleco
;
; @autor RuRuRu
; @date 2013/04/23 1st Release
;

	cseg
	org	0400h

stack:	equ	0f380h		; スタックエリア
mdata:	equ	0c000h		; 曲データアドレス

; メインループ

main:
	di
	im	1
	ld	sp, stack
	call	init
	ei
	jp	loop

loop:
	in	a, (02h)	; キー入力待ち
	or	a
	jr	z, loop

	cp	01h
	jr	nz, stop

	in	a, (04h)
	cp	01h
	jr	z, play_se

	di
	ld	a, 0C3h
	ld	(0FD9Fh),a

	in	a,(03h)
	add	a,a
	ld	l,a
	ld	h,000H
	ld	de,mustbl
	add	hl,de
	ld	a,(hl)
	inc	hl
	ld	h,(hl)
	ld	l,a

	ld	de,play_end
	push	de
	ei
	jp	(hl)

play_se:

	in	a,(03h)
	add	a,a
	ld	l,a
	ld	h,000H
	ld	de,setbl
	add	hl,de
	ld	a,(hl)
	inc	hl
	ld	h,(hl)
	ld	l,a

	in	a,(05h)
	ld	e, a

	xor	a			; ch #0
	ld	d, a
	call	06B4Eh
	jr	play_end

stop:
	call	05160h

play_end:
	xor	a		; 停止処理
	out	(02h), a
	jr	loop

init:
	ld	a, 0c9h
	ld	(0FD9Fh), a
	ld	hl, irq
	ld	(0FDA0h), hl

	ld	a, 0F7h		; RAM/ROM/RAM/RAM
	out	(0A8h), a

	ld	hl,04002h
	ld	de,08002h
	ld	bc,03FFEh
	ldir

	ld	a, 0FFh		; RAM/RAM/RAM/RAM
	out	(0A8h), a

	ld	hl,08002h
	ld	de,04002h
	ld	bc,03FFEh
	ldir

	ld	a,00Fh
	ld	(07D38h),a
	call	05AA7h
	ret

irq:
	call	05805h
	call	056A4h			; BGM
	ret

mustbl:
	dw	05154h		; title
	dw	0515Ah		; bgm
	dw	0514Eh		; stage clear

setbl:
	dw	04FA6h		; 32
	dw	04FA6h		; 00/64
	dw	04F5Ch		; 00/64/28
	dw	0506Bh		; 00
	dw	04E97h		; 00
	dw	04EE5h		; 00
	dw	05029h		; 00
	dw	05095h		; 00
	dw	04FE9h		; 00
	dw	04F2Eh		; 00
	dw	04DC2h		; 70/B0/E0
	dw	04FC6h		; 00

