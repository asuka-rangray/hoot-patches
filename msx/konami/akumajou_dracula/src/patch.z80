; 悪魔城ドラキュラ(CRC32:038c250d)
;
; @autor RuRuRu
; @date 2013/02/27 1st Release
;

	cseg
	org	0400h

stack:	equ	0f0f1h		; スタックエリア
stopc:	equ	0f1h		; 停止コード
sndbnk:	equ	00eh		; サウンドバンク
sndini:	equ	0507dh		; 初期化
sndply:	equ	050a6h		; サウンドコード発行
sndirq:	equ	08964h		; サウンド割り込み

main:
	di
	im	1
	ld	sp,stack
	call 	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	in	a,(03h)		; Code No.
	call	sndply
	jr	play_end

stop:
	ld	a, stopc
	call	sndply

play_end:
	xor	a		; hoot status変更
	out	(02h),a

	jr	loop

chg_bank:
	push	af
	ld	a, sndbnk
	ld	(08000h), a
	inc	a
	ld	(0A000h), a
	pop	af
	ret

rst_bank:
	push	af
	ld	a,(0F0F2H)
	ld	(08000h), a
	ld	a,(0F0F3H)
	ld	(0A000h), a
	pop	af
	ret

init:
; bank change
	ld	a,001H
	ld	(0F0F1H),a	; 6000~
	inc	a
	ld	(0F0F2H),a	; 8000~
	inc	a
	ld	(0F0F3H),a	; A000~

	ld	a, 0c3h
	ld	(0FD9Fh), a
	ld	hl, irq
	ld	(0FDA0h), hl

	call	sndini
	ret

irq:
	di
	call	chg_bank
	call	sndirq
	call	rst_bank
	ret

