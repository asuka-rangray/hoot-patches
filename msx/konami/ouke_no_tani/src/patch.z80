; 王家の谷(CRC32:5a141c44)
;
; @autor RuRuRu
; @date 2018/10/21 1st Release
;

	cseg
	org	0400h

stack:	equ	0f380h

main:
	di
	im	1
	ld	sp,stack
	call 	init
	ei

loop:
	in	a,(02h)		; Wait key
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	in	a, (04h)
	cp	01h
	jr	nz, no_stop

	call	play_stop
	halt

no_stop:
	xor	a
	ld	(0E03Bh),a	; Clear flag for code 01

	in	a,(03h)
	call	07AA9h
	jr	play_end

stop:
	call	play_stop

play_end:
	xor	a		; Change hoot status
	out	(02h),a
	jr	loop

play_stop:
	ld	a, 020h
	call	07AA9h
	ret

init:
	ld	a, 0C3h
	ld	(0FD9Fh), a
	ld	hl, irq
	ld	(0FDA0h), hl

	ld	a,0B8h
	call	07B2Dh

	call	play_stop

	ret

irq:
	di
	call	07B36h
	ei
	ret
