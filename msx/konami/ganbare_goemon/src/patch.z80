; がんばれゴエモン(CRC32:40e53ea3)
;
; @autor RuRuRu
; @date 2016/07/23 1st Release
;

	cseg
	org	0400h

stack:	equ	0F0F0h		; スタックエリア
stopc:	equ	00h		; 停止コード
sndbnk:	equ	0Ah		; サウンドバンク
sndini:	equ	04FB5h		; 初期化
sndin2:	equ	04129h		; 初期化2
sndply:	equ	04FE4h		; サウンドコード発行
sndirq:	equ	06000h		; サウンド割り込み


main:
	di
	im	1
	ld	sp,stack
	call 	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,play_stop

	in	a, (04h)
	cp	01h
	jr	nz, no_stop

	call	stop
	halt

no_stop:
	in	a,(03h)		; Code No.
	di
	call	chg_bank
	call	sndply
	call	rst_bank
	ei
	jr	play_end

stop:
	di
;	call	chg_bank
	ld	a, stopc
	call	sndply
;	call	rst_bank
	ei
	ret

play_stop:
	call	stop

play_end:
	xor	a		; hoot status変更
	out	(02h),a
	jr	loop

chg_bank:
	push	af
	ld	a, sndbnk
	ld	(06000h), a
	inc	a
	ld	(08000h), a
	inc	a
	ld	(0A000h), a
	pop	af
	ret

rst_bank:
	push	af
	call	04206h
	pop	af
	ret

init:
; bank change
	call	04206h

; timer entry
	ld	a, 0c3h
	ld	(0FD9Fh), a
	ld	hl, irq
	ld	(0FDA0h), hl

	call	sndini
	call	sndin2
	ret

irq:
	di
	call	chg_bank
	call	sndirq
	call	rst_bank
	ret
