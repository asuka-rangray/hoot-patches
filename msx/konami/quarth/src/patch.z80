; Quarth(CRC32:a0014e7f)
;
; @autor RuRuRu
; @date 2016/07/17 1st Release
;

	cseg
	org	0400h

stack:	equ	0f0f0h		; スタックエリア
stopc:	equ	01h		; 停止コード
sndbnk:	equ	04h		; サウンドバンク
sndini:	equ	04000h		; 初期化
sndply:	equ	04003h		; サウンドコード発行
sndirq:	equ	04006h		; サウンド割り込み


main:
	di
	im	1
	ld	sp,stack
	call 	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,play_stop

	in	a, (04h)
	cp	01h
	jr	nz, no_stop

	call	stop
	halt

no_stop:
	in	a,(03h)		; Code No.
	di
	call	chg_bank
	call	sndply
	call	rst_bank
	ei
	jr	play_end

stop:
	di
	call	chg_bank
	ld	a, stopc
	call	sndply
	call	rst_bank
	ei
	ret

play_stop:
	call	stop

play_end:
	xor	a		; hoot status変更
	out	(02h),a
	jr	loop

chg_bank:
	push	af
	ld	a, sndbnk
	ld	(05000h), a
	inc	a
	ld	(07000h), a
	inc	a
	ld	(09000h), a
	inc	a
	ld	(0B000h), a
	pop	af
	ret

rst_bank:
	push	af
	ld	a,(0E000H)
	ld	(05000h), a
	ld	a,(0E001H)
	ld	(07000h), a
	ld	a,(0E002H)
	ld	(09000h), a
	ld	a,(0E003H)
	ld	(0B000h), a
	pop	af
	ret

init:
; bank change
	ld	a,000H
	ld	(0E000H),a	; 4000~
	inc	a
	ld	(0E001H),a	; 6000~
	inc	a
	ld	(0E002H),a	; 8000~
	inc	a
	ld	(0E003H),a	; a000~

; timer entry
	ld	a, 0c3h
	ld	(0FD9Fh), a
	ld	hl, irq
	ld	(0FDA0h), hl

	call	chg_bank
	call	sndini
	call	rst_bank
	ret

irq:
	call	chg_bank
	call	sndirq
	call	rst_bank
	ret
