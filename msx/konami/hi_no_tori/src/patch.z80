; 火の鳥(CRC32:d95116b0)
;
; @autor RuRuRu
; @date 2016/07/23 1st Release
;

	cseg
	org	0400h

stack:	equ	0F0F0h		; スタックエリア
stopc:	equ	74h		; 停止コード
sndbnk:	equ	0Eh		; サウンドバンク
sndini:	equ	04166h		; 初期化
sndply:	equ	041C1h		; サウンドコード発行
sndirq:	equ	09497h		; サウンド割り込み


main:
	di
	im	1
	ld	sp, stack
	call 	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z, loop

	cp	01h
	jr	nz, play_stop

	in	a, (04h)
	cp	01h
	jr	nz, no_stop

	call	stop
	halt

no_stop:
	in	a, (03h)	; Code No.
	call	sndply
	jr	play_end

stop:
	ld	a, stopc
	call	sndply
	ret

play_stop:
	call	stop

play_end:
	xor	a		; hoot status変更
	out	(02h),a
	jr	loop

chg_bank:
	push	af
	ld	a, sndbnk
	ld	(08000h), a
	inc	a
	ld	(0A000h), a
	pop	af
	ret

rst_bank:
	push	af
	ld	a, (0F0F2H)
	ld	(08000h), a
	ld	a, (0F0F3H)
	ld	(0A000h), a
	pop	af
	ret

init:
; bank change
	call	053E9h

; timer entry
	ld	a, 0c3h
	ld	(0FD9Fh), a
	ld	hl, irq
	ld	(0FDA0h), hl

	call	sndini
	ret

irq:
	di
	call	chg_bank
	call	sndirq
	call	rst_bank
	ret
