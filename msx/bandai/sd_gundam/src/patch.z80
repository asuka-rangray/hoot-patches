; SD GUNDAM (c)BANPREST 用
; メインルーチン

	cseg
	org	0600h

stack:	equ	0f380h		; スタックエリア
mdata:	equ	0ceefh		; 曲データアドレス

; メインループ

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	call	00176h		; Stop
	halt

	di
	in	a,(03h)		; Code No.
	out	(03h), a
	ei

	in	a,(04h)		; Loop
	ld	e,a
	ld	hl, mdata
	call	00155h		; Play
	jr	loop

stop:
	call	00176h		; Stop
	halt
	call	00090h
	xor	a
	ld	(0CEEEH),a

play_end:
	xor	a
	out	(02h),a		; hoot status変更
	jr	loop

init:
	ld	a, 0C9h
	ld	(00024h), a

	ld	a, 0C3h
	ld	(0001Ch), a
	ld	(00038h), a
	ld	(00090h), a
	ld	(00093h), a
	ld	(00096h), a
	ld	hl, irq
	ld	(00039h), hl
	ld	hl, calslt
	ld	(0001Dh), hl
	ld	hl, psgini
	ld	(00091h), hl
	ld	hl, psgw
	ld	(00094h), hl
	ld	hl, psgr
	ld	(00097h), hl

	call	012Dh
;	call	02489h

	in	a,(07h)
	cp	001h
	jr	nz, init_end

	ld	a, 03Fh		; m-slot : ram/ram/ram/bios
	out	(0A8h),a
	ld	a, 04h
	ld	(0FFFFh),a	; s-slot : bios/fmbios/none/none
	ld	a, 0F3h		; m-slot : ram/fmbios/ram/ram
	out	(0A8h),a

	ld	(09600h), a	; OPLL flag
	ld	hl, 0D400h
	call	04113h

init_end:

	ret

calslt:
	di
	push	af
	push	bc

	push	af
	ld	a, 0F3h		; ram/fmbios/ram/ram
	out	(0A8h), a
	pop	af

	push	hl
	ld	hl, end_calslt
	push	hl
	pop	bc
	pop	hl

	push	bc
	jp	(ix)
end_calslt:
	ld	a, 0FFh		; ram/ram/ram/ram
	out	(0A8h), a
	pop	bc
	pop	af
	ret
;	jp	(ix)

psgini:
	ld	e,00h
	ld	a,08h
	call	psgw
	inc	a
	call	psgw
	inc	a
	call	psgw
	inc	a

	ld	e,0B8h
	ld	a,07
	call	psgw
	ret

psgw:
	di
	out     (0A0h),a
	push    af
	ld      a,e
	out     (0A1h),a
	ei
	pop     af
	ret

psgr:
	out     (0A0h),a
	in      a,(0A2h)
	ret

irq:
	push	hl
	push	de
	push	bc
	push	af
	exx
	ex	af,af'
	push	hl
	push	de
	push	bc
	push	af
	push	iy
	push	ix
	di
	in	a,(099h)
	or	a
	jp	p,int_end
	call	020DAh
	ei
int_end:
	pop	ix
	pop	iy
	pop	af
	pop	bc
	pop	de
	pop	hl
	ex	af,af'
	exx
	pop	af
	pop	bc
	pop	de
	pop	hl
	ei
	ret
