; 大障害競馬 (c)Casio 用
; メインルーチン

	cseg
	org	0400h

stack:	equ	0f370h		; スタックエリア

; メインループ

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	in	a, (04h)
	cp	01h
	jr	z, play_start

	call	play_stop

play_start:
	di

	in	a, (03h)	; Code No.
	add	a, a		; x2
	ld	b, a
	add	a, a		; x4
	add	a, b		; x6
	ld	e, a
	ld	d, 00h
	ld	hl, mustbl
	add	hl, de
	xor	a
	ld	c, a
	ld	b, 03h
pllp:
	ld	e,(hl)
	inc	hl
	ld	d,(hl)
	inc	hl
	ld	a,d
	cp	00h
	jr	z,ignr
	push	bc
	push	hl
	ld	b, c		; ch no
	call	080DBh		; Sound Req
	pop	hl
	pop	bc
ignr:
	inc	c
	djnz	pllp

	ei
	jr	play_end

stop:
	call	play_stop

play_end:
	xor	a		; hoot status変更
	out	(02h),a
	jr	loop

play_stop:
	call	08102h
	ret

init:
	ld	a, 0c3h
	ld	(0FD9Fh), a
;	ld	hl, 0805Bh	; sound irq
	ld	hl, 0813Ah	; sound irq
	ld	(0FDA0h), hl

	ret

mustbl:
	dw	0AD9BH
	dw	0BD89H
	dw	00000h

	dw	0BEDCH
	dw	0BF07H
	dw	00000h

	dw	0BDCFH
	dw	0BE02H
	dw	00000h

	dw	00000h
	dw	00000h
	dw	0BE8EH

	dw	0BE7FH
	dw	00000h
	dw	00000h

	dw	0BF7DH
	dw	0BF8AH
	dw	00000h

	dw	0BE56H
	dw	0BE6CH
	dw	00000h

	dw	00000h
	dw	0BE49H
	dw	0BE49H

	dw	00000h
	dw	0BF32H
	dw	0BF54H

	dw	00000h
	dw	0BF97H
	dw	0BFB9H

	dw	00000h
	dw	00000h
	dw	0BEA1H

	dw	00000h
	dw	0BEC9H
	dw	00000h

