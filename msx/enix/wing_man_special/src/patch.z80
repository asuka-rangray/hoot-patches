; Wing Man Special MSX2 (c)Enix 用
;
; @autor RuRuRu
; @date 2011/05/03 1st Release
;

	cseg
	org	0400h

stack:	equ	0f380h		; スタックエリア

; メインループ

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

; play
	in	a,(03h)		; Code No.

	di
	out	(03h), a
	ld	a, 0c9h
	ld	(0FD9Fh), a
	in	a, (04h)
	ld	(drv_type),a

	cp	01h
	jr	z, pl1
	cp	02h
	jr	z, pl2

; 汎用
	call	0841Fh
	jr	play_end

; SUNSET
pl1:
	ld	hl, 08400h
	ld	de, 0C000h
	ld	bc, 1400h
	ldir
	call	0C000h
	jr	play_end

; OPENING(FM)
pl2:
	ld	hl, 08400h
	ld	de, 0C000h
	ld	bc, 1400h
	ldir
	call	0C062h		; Call Tone
	ld	a, 0C9h
	ld	(0C01Dh), a
	call	0C000h
	jr	play_end

stop:
	di
	call	090h
	ld	a, 0C9h
	ld	(0FD9Fh), a

; OPLLは仕方ないので自前消音
	ld	a, 0Eh
	out	(7Ch), a
	xor	a
	out	(7Dh), a

	ld	c, 020h
	ld	b, 008h
fmlp:
	ld	a, c
	out	(7Ch), a
	ld	a, 00h
	out	(7Dh), a
	inc	c
	djnz	fmlp

	ld	c, 030h
	ld	b, 008h
fmlp2:
	ld	a, c
	out	(7Ch), a
	ld	a, 0FFh
	out	(7Dh), a
	inc	c
	djnz	fmlp2

play_end:
	ei
	xor	a		; hoot status変更
	out	(02h),a
	jr	loop

init:
	ld	a, 0c9h
	ld	(0FD9Fh), a
	ret

drv_type:
	db	00h
