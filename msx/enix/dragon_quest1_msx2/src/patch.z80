; Dragon Quest MSX2 (c)Enix 用
;
; @autor RuRuRu
; @date 2011/02/16 1st Release
;

	cseg
	org	0400h

stack:	equ	0f380h		; スタックエリア

; メインループ

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	in	a,(03h)		; Code No.
	call	0D000h

play_end:
	xor	a
	out	(02h),a		; hoot status変更
	jr	loop

play_stop:
	xor	a
	call	0D000h
	ret

stop:
	call	play_stop
	jp	play_end

init:
	ld	a, 0c3h
	ld	(0FD9Fh), a
	ld	hl, 0D003h
	ld	(0FDA0h), hl

; ルーチンコピー
	ld	a, 0Ah
	ld	(07000H),a
	ld	hl,08000h
	ld	de,0D000h
	ld	bc,02000h
	ldir

	ld	a, 04h
	ld	(07000H),a
	inc	a
	ld	(07800H),a

; Ending Data Table
	ld	hl,0B0F2h
	ld	de,0D5DEh
	ld	bc,00006h
	ldir

	ret
