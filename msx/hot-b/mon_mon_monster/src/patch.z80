; 悶々怪物 (c)Hot-B
;
; @autor RuRuRu
; @date 2010/06/18 1st Release
; @date 2011/04/13 Fix loop flag
;

	cseg
	org	0400h

stack:	equ	0f380h		; スタックエリア

; メインループ

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	in	a,(04h)
	cp	01h
	jr	z,play_se

; play
	call	04EB6h		; Stop

	in	a,(05h)		; Loop flag
	ld	(flgloop),a

	in	a,(03h)		; Code No.
	ld	(songno), a

	call	04E9Ah		; Req

	jr	play_end

play_se:
	di
	xor	a
	ld	(flgloop),a
	in	a,(03h)		; Code No.
	call	04E8Bh
	ei
	jr	play_end

stop:
	di
	xor	a
	ld	(flgloop),a
	call	04EB6h		; Stop
	ei

play_end:
	xor	a		; hoot status変更
	out	(02h),a
	jr	loop

init:
; Regist entry
	ld	a, 0c3h
	ld	(0FD9Fh), a
	ld	hl, irq
	ld	(0FDA0h), hl

; OPLL/PSG
	in	a, (07h)
	ld	(0E00Dh), a
	ld	(0E00Ch), a

	ret

irq:
	call	04CEBh		; Sound irq
	ld	a, (flgloop)
	cp	01h
	jr	nz, irq_end
	ld	a, (songno)
	ld	(0E282h), a
irq_end:
	ret


flgloop:
	db	00h
songno:
	db	00h
