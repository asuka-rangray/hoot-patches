; Super上海 Dragon's Eye(OPLL) (c)Hot-B
;
; @autor RuRuRu
; @date 2010/05/23 1st Release
;

	cseg
	org	00400h

stack:	equ	0f380h		; スタックエリア
mdata:	equ	08200h		; 曲データアドレス

; メインループ

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei
	jp	loop

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	call	04119h		; Stop

	di
	ld	de, mdata
	in	a,(03h)
	add	a,a
	add	a,a
	ld	l,a
	ld	h,000H
	add	hl,de
	push	hl
	pop	de

	ld	a,(de)
	ld	l,a
	inc	de
	ld	a,(de)
	add	a,082h		; +8200h
	ld	h,a
	ei

	call	04116h

	xor	a
	out	(02h),a		; 処理終了通知
	jr	loop

stop:
	call	04119h		; Stop

	xor	a		; 処理終了通知
	out	(02h),a
	jr	loop

init:
	ld	a, 0c3h
	ld	(0038h), a

	ld	hl, timer
	ld	(0039h), hl

	ld	hl,02000h
	call	04113h

	ret

timer:
	push	hl
	push	de
	push	bc
	push	af
	exx
	ex	af,af'
	push	hl
	push	de
	push	bc
	push	af
	push	iy
	push	ix
	call	0411Fh
	pop	ix
	pop	iy
	pop	af
	pop	bc
	pop	de
	pop	hl
	ex	af,af'
	exx
	pop	af
	pop	bc
	pop	de
	pop	hl
	ei
	ret
