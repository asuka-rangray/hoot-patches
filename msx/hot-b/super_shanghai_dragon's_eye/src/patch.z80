; Super上海 Dragon's Eye(PSG) (c)Hot-B
;
; @autor RuRuRu
; @date 2010/06/25 1st Release
;

	cseg
	org	00400h

stack:	equ	0f380h		; スタックエリア
mdata:	equ	08200h		; 曲データアドレス

; メインループ

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei
	jp	loop

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	call	02DA1h		; Stop

	di
	ld	de, 09000h
	in	a,(03h)
	add	a,a
	add	a,a		; x4
	ld	l,a
	ld	h,000H
	add	hl,de
	push	hl
	pop	de

	ld	a,(de)
	ld	l,a
	inc	de
	ld	a,(de)
	add	a,090h		; +9000h
	ld	h,a
	ei

	ld	de,08200h
	ld	bc,0c00h
	ldir

	ld	hl,mdata
	call	02D61h

	xor	a
	out	(02h),a		; 処理終了通知
	jr	loop

stop:
	call	02DA1h		; Stop

	xor	a		; 処理終了通知
	out	(02h),a
	jr	loop

init:
	ld	a, 0c3h
	ld	(001Ch), a
	ld	(0038h), a
	ld	(0090h), a

	ld	hl, calslt
	ld	(001Dh), hl

	ld	hl, timer
	ld	(0039h), hl

	ld	hl, psgini
	ld	(0091h), hl

	ld	a, 0C9h
	ld	(02D55H), a

	call	02DA1h		; Stop

	ret

calslt:
	jp	(ix)

psgini:
	ld	e,00h
	ld	a,08h
	call	psgw
	inc	a
	call	psgw
	inc	a
	call	psgw
	inc	a

	ld	e,0B8h
	ld	a,07h
	call	psgw
	ret

psgw:
	di
	out     (0A0h),a
	push    af
	ld      a,e
	out     (0A1h),a
	ei
	pop     af
	ret

timer:
	push	hl
	push	de
	push	bc
	push	af
	exx
	ex	af,af'
	push	hl
	push	de
	push	bc
	push	af
	push	iy
	push	ix
	call	02DBFh
	pop	ix
	pop	iy
	pop	af
	pop	bc
	pop	de
	pop	hl
	ex	af,af'
	exx
	pop	af
	pop	bc
	pop	de
	pop	hl
	ei
	ret
