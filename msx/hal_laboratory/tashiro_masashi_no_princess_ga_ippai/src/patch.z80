; 田代まさしのプリンセスがいっぱい (c)1989 HAL研究所
;
; @autor RuRuRu
; @date 2015/05/05 1st Release
; @date 2020/07/05 Support Title & Ending
;

	cseg
	org	0e000h

stack:	equ	0f400h

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei

loop:
	in	a,(02h)		; Key wait
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	in	a,(04h)		; Driver kind
	cp	01h
	jr	z, pcom_play

; play
	in	a,(03h)		; Code No.
	push	af
	ld	a, 00h
	out	(03h),a
	pop	af
play:
	ld	(08B62h),a
	call	088BEh		; Play

	ld	a, 0C3h
	ld	(0FD9Fh), a
	ld	hl, 080C0h
	ld	(0FDA0h), hl
	jr	play_end

pcom_play:
	in	a,(03h)		; Code No.
	push	af
	ld	a, 01h
	out	(03h),a
	pop	af
	ld	(0B916h),a
	call	0B672h		; Play

	ld	a, 0C3h
	ld	(0FD9Fh), a
	ld	hl, 0AE74h
	ld	(0FDA0h), hl
	jr	play_end

stop:
	ld	a, 0C9h
	ld	(0FD9Fh), a
	call	090h
	jr	play_end

play_end:
	xor	a		; Change hoot status
	out	(02h),a
	jr	loop

init:
	ld	a, 0C9h
	ld	(0FD9Fh), a

	ld	a, 0C3h
	ld	(0001Ch), a
	ld	(00020h), a
	ld	(00038h), a
	ld	(00090h), a
	ld	(00093h), a
	ld	hl, dcompr
	ld	(00021h), hl
	ld	hl, irq
	ld	(00039h), hl
	ld	hl, gicini
	ld	(00091h), hl
	ld	hl, psgw
	ld	(00094h), hl

	ret

dcompr:
	ld      a,h
	cp      d
	ret     nz
	ld      a,l
	cp      e
	ret

gicini:
	push	af
	push	de
	ld	e,00h
	ld	a,08h
	call	psgw
	inc	a
	call	psgw
	inc	a
	call	psgw
	inc	a

	ld	e,0B8h
	ld	a,07
	call	psgw

	pop	de
	pop	af
	ret

psgw:
	di
	out     (0A0h),a
	push    af
	ld      a,e
	out     (0A1h),a
	ei
	pop     af
	ret

irq:
	push	hl
	push	de
	push	bc
	push	af
	exx
	ex	af,af'
	push	hl
	push	de
	push	bc
	push	af
	push	iy
	push	ix
	di
	in	a,(099h)
	or	a
	jp	p,int_end
	call	0FD9Fh
	ei
int_end:
	pop	ix
	pop	iy
	pop	af
	pop	bc
	pop	de
	pop	hl
	ex	af,af'
	exx
	pop	af
	pop	bc
	pop	de
	pop	hl
	ei
	ret
