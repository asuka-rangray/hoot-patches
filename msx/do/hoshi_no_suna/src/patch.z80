; 星の砂 (c)D.O. 用
;
; @autor RuRuRu
; @date 2013/02/02 1st Release
;

	cseg
	org	0800h

stack:	equ	0f380h		; スタックエリア
mdata:	equ	008FA6h		; 曲データアドレス

; メインループ

main:
	di
	im	1
	ld	sp,stack
	call	init
	ei
	jp	loop

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	ld	a,003h
	call	0817Ah

	di
	in	a,(03h)
	out	(03h),a		; 曲データを読み込む
	ei

	ld	a,002h
	ld	hl,mdata
	call	0817Ah

	jr	loop

stop:
	ld	a,003h
	call	0817Ah

	xor	a		; 停止処理
	out	(02h),a
	jr	loop

init:
	ld	a, 0c9h
	ld	(8279h), a
	ld	(0090h), a

	ld	a, 0c3h
	ld	(0038h), a
	ld	(001Ch), a

	ld	hl, timer
	ld	(0039h), hl

	ld	hl, calslt
	ld	(001Dh), hl

	in	a,(07h)
	and	01h
	add	a, 02h
	ld	(08274h), a

	ret

calslt:
	jp	(ix)

timer:
	push	hl
	push	de
	push	bc
	push	af
	exx
	ex	af,af'
	push	hl
	push	de
	push	bc
	push	af
	push	iy
	push	ix
	di
	in	a,(099h)
	or	a
	jp	p,int_end
	call	085AEh
	ei
int_end:
	pop	ix
	pop	iy
	pop	af
	pop	bc
	pop	de
	pop	hl
	ex	af,af'
	exx
	pop	af
	pop	bc
	pop	de
	pop	hl
	ei
	ret
