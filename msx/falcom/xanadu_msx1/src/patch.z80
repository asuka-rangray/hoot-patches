; Xanadu MSX1 (c)Falcom
;
; @autor RuRuRu
; @date 2010/03/23 1st Release
;

	cseg
	org	0400h

stack:	equ	0f380h		; スタックエリア

; メインループ

main:
	di
	im	1
	ld	sp,stack
	call 	init
	ei

loop:
	in	a,(02h)		; キー入力待ち
	or	a
	jr	z,loop

	cp	01h
	jr	nz,stop

	in	a,(04h)		; Code No.
	cp	01
	jr	z, play_se

	ld	a, 02h		; stop
	call	04119h

	di
	in	a,(03h)		; Code No.
	ld	(0F1A5H),a
	ld	a, 01h		; play
	call	04119h
	ei

	jp	play_end

play_se:
	in	a,(03h)		; Code No.
	call	04148h

play_end:
	xor	a
	out	(02h),a		; hoot status変更
	jr	loop

stop:
	di
	ld	a, 02h
	call	04119h
	ei

	xor	a		; hoot status変更
	out	(02h),a

	jr	loop

init:
	call	04165h
	call	04960h

; timer entry
	ld	a, 0c3h
	ld	(0FD9Fh), a
	ld	hl, irq
	ld	(0FDA0h), hl

	ret

irq:
	di
	ld	a,002H
	ld	(07800H),a	; A000~BF000 -> Bank2
	call	0A000H		; IRQ
	ei
	ret

